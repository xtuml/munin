private service JobManagement::clearDomain () is
storedJobIdentifiers : sequence of instance of StoredJobIdentifier;
auditEvents : sequence of instance of AuditEvent;
unassignedJobs : sequence of instance of UnassignedJob;
assignedJobs : sequence of instance of AssignedJob;
jobs : sequence of instance of Job;
retiredWorkers : sequence of instance of RetiredWorker;
employedWorkers : sequence of instance of EmployedWorker;
workers : sequence of instance of JobWorker;
emptyUnassignedJobIds : sequence of string;
jobManager : instance of JobManager;

begin
	
	// clear down the domain of all instance except Job Management Spec, Job Manager and Job Store
	storedJobIdentifiers := find StoredJobIdentifier();
	unlink storedJobIdentifiers R20;
	delete storedJobIdentifiers;
	
	auditEvents := find AuditEvent();
	unlink auditEvents R7;
	delete auditEvents;
	
	unassignedJobs := find UnassignedJob();
	assignedJobs := find AssignedJob();
	jobs := find Job();
	unlink unassignedJobs R5.Job;
	unlink assignedJobs R5.Job;
	unlink assignedJobs R6;
	delete unassignedJobs;
	delete assignedJobs;
	delete jobs;
	
	retiredWorkers := find RetiredWorker();
	employedWorkers := find EmployedWorker();
	workers := find JobWorker();
	unlink retiredWorkers R2.JobWorker;
	unlink employedWorkers R2.JobWorker;
	unlink workers R3.employedAfter.JobWorker;
	unlink workers R3.employedBefore.JobWorker;
	unlink workers R1;
	unlink workers R4;
	unlink workers R21;
	unlink workers R22;
	delete retiredWorkers;
	delete employedWorkers;
	delete workers;
	
	jobManager := find_one JobManager();
	jobManager.unassignedJobIds := emptyUnassignedJobIds;
	
end service;
